module docs

import strings
import lists

func docsToMarkdown { docs =>
    with docs, type TypeDocs {
        ModuleDocs: { d =>
            strings.concat [
                "# ", d.name, "\n\n",
                "_module_ ", d.docs, "\n\n",
                (strings.join "\n" , (lists.map moduleMemberDocsToMarkdown, d.types)),
                "\n\n",
                (strings.join "\n" , (lists.map docsToMarkdown, d.types))
            ]
        },
        EnumDocs: { d => 
            strings.concat [
                "## ", d.name, "\n\n",
                "_enum_ ",
                d.docs, 
                (if d.cases == Nil, "", strings.concat [
                    "\n\n",
                    "### Cases\n\n",
                    (strings.join "\n" , (lists.map enumCaseDocsToMarkdown, d.cases)),
                    "\n"
                ])
            ]
        },
        DataDocs: { d => 
            strings.concat [
                "## ", d.name, "\n\n",
                "_data_ ",
                d.docs,
                (if d.fields == Nil, "", strings.concat [
                    "\n\n",
                    "### Properties\n\n",
                    (strings.join "\n" , (lists.map dataFieldDocsToMarkdown, d.fields)),
                    "\n"
                ])
            ]
        },
        FunctionDocs: { d =>
            strings.concat [
                "## ", d.name,"\n\n"
                "_func_ `",
                d.name,
                " ",
                (strings.join ", ", d.params),
                "`\n\n",
                d.docs
            ]
        },
        ConstantDocs: { d =>
            strings.concat [
                "## ", d.name,"\n\n"
                "_let_ ",
                d.docs, "\n"
            ]
        },
        ExternTypeDocs: { d =>
            strings.concat [
                "## ", d.name,"\n\n"
                "_extern_ ",
                d.docs, "\n"
            ]
        },
        ExternFunctionDocs: { d =>
            strings.concat [
                "## ", d.name,"\n\n"
                "_extern_ ",
                d.docs, "\n"
            ]
        },
        None: { _ => "" }
    }
}

func moduleMemberDocsToMarkdown { member =>
    with member, type TypeDocs {
        ModuleDocs: { d =>
            strings.concat [
                "- _module_ [", d.name, "](#", d.name, ")"
            ]
        },
        EnumDocs: { d => 
            strings.concat [
                "- _enum_ [", d.name, "](#", d.name, ")"
            ]
        },
        DataDocs: { d => 
            strings.concat [
                "- _data_ [", d.name, "](#", d.name, ")"
            ]
        },
        FunctionDocs: { d =>
            strings.concat [
                "- _func_ [", d.name, "](#", d.name, ")",
                " ",
                (strings.join ", ", d.params)
            ]
        },
        ConstantDocs: { d =>
            strings.concat [
                "- _let_ [", d.name, "](#", d.name, ")"
            ]
        },
        ExternTypeDocs: { d =>
            strings.concat [
                "- _extern_ [", d.name, "](#", d.name, ")"
            ]
        },
        ExternFunctionDocs: { d =>
            strings.concat [
                "- _extern_ [", d.name, "](#", d.name, ")"
            ]
        },
        None: { _ => "" }
    }
}

func enumCaseDocsToMarkdown { case =>
    strings.concat [
        "- [", case.name, "](#", case.type.name, ")", markdownDocsSuffix case.docs
    ]
}

func dataFieldDocsToMarkdown { field =>
    with field.params, type List {
        Cons: { _ => 
            strings.concat ["- `", field.name, " ", (strings.join ", ", field.params), "`", markdownDocsSuffix field.docs]
        },
        Nil: { _ => 
            strings.concat ["- `", field.name, "`", markdownDocsSuffix field.docs]
        }
    }
}

func markdownDocsSuffix { docsString =>
    if docsString == "", "", strings.concat [" - ", docsString]
}
