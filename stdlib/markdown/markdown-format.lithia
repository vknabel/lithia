/// Implements the markdown format.
/// Currently only handles converting markup.Markup to markdown strings.
/// Planned to be able of parsing markdown strings to markup.Markup in the future.
module markdown

import markup
import strings
import lists
import ranges

/// The default markup.Format to generate markdown from markup.
let format = markup.Format convert

/// Converts markup.Markup to a markdown string.
func convert { =>
    type markup.Markup {
        String: { str => str },
        List: pipe [
            lists.map markdown.convert,
            strings.concat
        ],
        Heading: { hx =>
            let prefix = strings.concat (
                lists.replicate hx.level, "#"
            )
            strings.concat [
                prefix,
                " ",
                markdown.convert hx.child,
                "\n\n"
            ]
        },
        Paragraph: { b =>
            let paragraph = with b.child, type markup.Markup {
                List: pipe [
                    lists.map markdown.convert,
                    strings.join "\n"
                ],
                Any: { _ => markdown.convert b.child }
            }
            strings.concat [
                paragraph,
                "\n\n"
            ]
        },
        Italic: { n =>
            strings.concat [
                "_",
                markdown.convert n.child,
                "_",
            ]
        },
        Bold: { n =>
            strings.concat [
                "**",
                markdown.convert n.child,
                "**",
            ]
        },
        Link: { n =>
            strings.concat [
                "[",
                markdown.convert n.child,
                "]",
                "(",
                n.url,
                ")",
            ]
        },
        Image: { n =>
            strings.concat [
                "![",
                markdown.convert n.alt,
                "]",
                "(",
                n.url,
                ")",
            ]
        },
        Code: { n =>
            strings.concat [
                "`",
                markdown.convert n.text,
                "`",
            ]
        },
        CodeBlock: { n =>
            let language = with n.language, type Maybe {
                Some: { some => some.value },
                None: { none => "" },
                Any: { any => any }
            }
            strings.concat [
                "```",
                language,
                "\n",
                n.text,
                "\n```\n\n",
            ]
        },
        UnorderedList: { n =>
            let items = lists.map pipe [
                markdown.convert,
                "- ".append
            ], n.children

            strings.concat [
                (strings.join "\n", items),
                "\n\n"
            ]
        },
        OrderedList: { n =>
            let items = lists.zipWith { i, child =>
                strings.concat [
                    i, ". ",
                    markdown.convert child,
                ]
            }, ranges.indices, n.children

            strings.concat [
                (strings.join "\n", items),
                "\n\n"
            ]
        },
    }
}
